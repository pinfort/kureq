include protocol
include url

+class Request()
	var protocol: @Protocol
	+var curl: \previous_research\pinfort\kuurl\src\curl@Curl
	
	*func ctor()
		do me.protocol :: #@Protocol
		do me.curl :: \previous_research\pinfort\kuurl\src\curl@makeCurl()
	end func
	
	func init(): \previous_research\pinfort\kuurl\src\curl@Curl
		do me.curl :: \previous_research\pinfort\kuurl\src\curl@makeCurl()
	end func
	
	+func clean()
		do me.curl.easy_cleanup()
	end func
	
	func reset()
		do me.curl.easy_reset()
	end func
	
	func errCheck(responseCode: int)
		var curle_ok: \previous_research\pinfort\kuurl\src\curl@CurlCode :: %OK
		
		if(responseCode = (curle_ok $ int))
			; 正常。何もせずに戻る
			ret
		end if
		
		; 異常。throwして停止
		var curlCodeName: \previous_research\pinfort\kuurl\src\curl@CurlCodeName :: #\previous_research\pinfort\kuurl\src\curl@CurlCodeName
		do cui@print("ERRORCODE: " ~ responseCode.toStr() ~ "\n")
		do cui@print("ERROR: " ~ curlCodeName.fromCode(responseCode) ~ "\n")
		do me.clean()
		throw excpt@userMin {TODO: エラーコードを考える}
	end func
	
	func perform(): \previous_research\pinfort\kuurl\src\curl@Response
		var response: \previous_research\pinfort\kuurl\src\curl@Response :: #\previous_research\pinfort\kuurl\src\curl@Response
		do me.curl.config_easy_response(&response)
		do me.curl.config_easy_header(&response)
		
		var resCode: int :: me.curl.easy_perform()
		do me.errCheck(resCode)
		do me.reset()
		ret response
	end func
	
	+func get(url: []char): []char
		var curlopt_url: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %URL
		do me.curl.easy_setopt_str(curlopt_url $ int, url)
		
		var response: \previous_research\pinfort\kuurl\src\curl@Response :: me.perform()
		
		var res: []char :: response.toStr()
		
		do me.reset()
		ret res
	end func
	
	+func getResponse(url: []char): \previous_research\pinfort\kuurl\src\curl@Response
		var curlopt_url: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %URL
		do me.curl.easy_setopt_str(curlopt_url $ int, url)
		
		var curlopt_redirect: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %FOLLOWLOCATION
		do me.curl.easy_setopt_int(curlopt_redirect $ int, 1)
		
		var response: \previous_research\pinfort\kuurl\src\curl@Response :: me.perform()
		
		do me.reset()
		ret response
	end func
	
	+func head(url: []char): []char
		var curlopt_url: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %URL
		do me.curl.easy_setopt_str(curlopt_url $ int, url)
		
		var curlopt_head: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %NOBODY
		do me.curl.easy_setopt_int(curlopt_head $ int, 1)
		
		var response: \previous_research\pinfort\kuurl\src\curl@Response :: me.perform()
		
		var res: []char :: response.toStr()
		
		do me.reset()
		ret res
	end func
	
	+func post(url: []char, postdata: []char): []char
		var curlopt_url: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %URL
		do me.curl.easy_setopt_str(curlopt_url $ int, url)
		
		
		var curlopt_verbose: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %VERBOSE
		do me.curl.easy_setopt_int(curlopt_verbose $ int, 1)
		
		; POSTFIELDS not work because valiable will be GCed.
		var curlopt_post: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %COPYPOSTFIELDS
		do me.curl.easy_setopt_str(curlopt_post $ int, postdata)
		
		var response: \previous_research\pinfort\kuurl\src\curl@Response :: me.perform()
		
		var res: []char :: response.toStr()
		do me.reset()
		ret res
	end func
	
	+func postBin(url: []char, postdata: []bit8): []char
		var curlopt_url: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %URL
		do me.curl.easy_setopt_str(curlopt_url $ int, url)
		
		
		var curlopt_verbose: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %VERBOSE
		do me.curl.easy_setopt_int(curlopt_verbose $ int, 1)
		
		var curlopt_postlen: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %POSTFIELDSIZE
		do me.curl.easy_setopt_int(curlopt_postlen $ int, ^postdata)
		
		; POSTFIELDS not work because valiable will be GCed.
		var curlopt_post: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %COPYPOSTFIELDS
		do me.curl.easy_setopt_arr(curlopt_post $ int, postdata)
		
		var curlopt_post_header: \previous_research\pinfort\kuurl\src\curl@CurlOption :: %HTTPHEADER
		var slist: \previous_research\pinfort\kuurl\src\curl@Slist :: \previous_research\pinfort\kuurl\src\curl@makeSlist("Content-Type: application/octet-stream")
		do slist :: slist.append("pragma: no-cache")
		do me.curl.easy_setopt_slist(curlopt_post_header $ int, slist)
		
		var response: \previous_research\pinfort\kuurl\src\curl@Response :: me.perform()
		
		var res: []char :: response.toStr()
		do slist.free_all()
		do me.reset()
		ret res
	end func
end class
